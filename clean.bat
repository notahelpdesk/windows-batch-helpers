:: clean.bat
::
:: The purpose of this is to remove files generated by the build process
:: It is a lighter-weight version of clean-everything.bat, whose purpose is to remove everything from a repo
::
@echo off
setlocal ENABLEDELAYEDEXPANSION
set dirPushed=false

if NOT "%1" == "" (
    pushd %1
    set dirPushed=true
)

call :removeDirectory bin
call :removeDirectory obj
call :removeDirectory .terraform
call :removeDirectory Reports

if "!dirPushed!" == "true" (
    popd
)
goto :END

:removeDirectory
set dirFound=false
for /f "Tokens=* Usebackq" %%F  IN (`dir /s /b /ad %1 2^>nul`) DO SET dirFound=true
if "!dirFound!"=="true" (
    for /f "Tokens=*" %%G IN ('dir /s /b /ad %1') do (
        ::echo Removing %%G
        rd /s /q %%G
    )
    if exist %1 rd /q %1
)

:END
endlocal